ext {
  mainTestClass = "com.monstrous.gdx.tests.webgpu.WebGPUTestStarter"
}

apply from: "../../dependencies.gradle"

sourceSets.main.resources.srcDirs = ["../assets"]

if (JavaVersion.current().isJava9Compatible()) {
  compileJava {
    options.release = versions.java
  }
}

dependencies {
  implementation project(":gdx-webgpu2")
  implementation project(":backends:gdx-desktop-webgpu")
  implementation project(":tests:gdx-tests-webgpu")
}

tasks.register('launchTestsWebGPU', JavaExec) {
  dependsOn classes
  mainClass = mainTestClass
  classpath = sourceSets.main.runtimeClasspath
  standardInput = System.in
  workingDir = new File("../assets")
  ignoreExitValue = true as JavaExecSpec
}
configure (launchTestsWebGPU) {
  group "LibGDX"
  description = "Run the WebGPU tests"
}
tasks.register('dist', Jar) {
  dependsOn classes
  manifest {
    attributes 'Main-Class': project.mainTestClass
  }
  dependsOn configurations.runtimeClasspath
  from {
    configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
  }
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
  with jar
}
